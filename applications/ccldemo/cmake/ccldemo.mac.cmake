include_guard (DIRECTORY)

list (APPEND ccldemo_ccl_sources
	${CCL_DIR}/platform/cocoa/cocoamain.mm
)

list (APPEND ccldemo_bundle_resources
	${CMAKE_CURRENT_LIST_DIR}/../packaging/mac/resource/ccldemo.xcassets
	${CCL_DIR}/packaging/mac/resource/MainMenu.xib
)

list (APPEND ccldemo_resources
	${ccldemo_bundle_resources}
)

ccl_add_shader_resource (ccldemo ${CMAKE_CURRENT_LIST_DIR}/../resource/shaders/vertexshader.metal PATH shaders)
ccl_add_shader_resource (ccldemo ${CMAKE_CURRENT_LIST_DIR}/../resource/shaders/pixelshader.metal PATH shaders)

list (APPEND ccldemo_sources
	${ccldemo_resources}
)
source_group ("Resources" FILES ${ccldemo_resources})

set (ccldemo_FRAMEWORKS
	ccltext
	cclsystem
	cclgui
	cclnet
	cclsecurity
)

set (ccldemo_PLUGINS
	modelimporter3d
)

# configure entitlements
set (entitlements_input_file "${CMAKE_CURRENT_LIST_DIR}/../packaging/mac/ccldemo.entitlements.in")
set (entitlements_output_file "${CMAKE_CURRENT_BINARY_DIR}/packaging/ccldemo.entitlements")

if (${CCLDEMO_MAC_ENABLE_SANDBOX})
	set (entitlements_enable_sandbox "true")
else ()
	set (entitlements_enable_sandbox "false")
endif ()

if (${CCLDEMO_CCLSPY_SUPPORT})
	add_plugin_to_mac_debug_bundle(ccldemo cclspy)
endif ()

configure_file (${entitlements_input_file} ${entitlements_output_file})

# set packaging properties
set_target_properties (ccldemo PROPERTIES
	RESOURCE "${ccldemo_bundle_resources}"
	MACOSX_BUNDLE_INFO_PLIST "${CMAKE_CURRENT_LIST_DIR}/../packaging/mac/Info.plist"
	XCODE_ATTRIBUTE_ASSETCATALOG_COMPILER_APPICON_NAME "AppIcon"

	XCODE_ATTRIBUTE_CODE_SIGN_IDENTITY "Apple Development"
	XCODE_ATTRIBUTE_CODE_SIGN_ENTITLEMENTS "${entitlements_output_file}"
)

if (${VENDOR_USE_PUBLISHER_CERTIFICATE})
	set_target_properties (ccldemo PROPERTIES
		XCODE_ATTRIBUTE_CODE_SIGN_STYLE "Manual"
		XCODE_ATTRIBUTE_CODE_SIGN_IDENTITY "${SIGNING_CERTIFICATE_MAC}"
	)
endif ()
